set dotenv-load := false

default:
  @just --list

install:
  pip install -U -r requirements.dev.txt

lint:
	isort . && black . && mypy fief/ && tryceratops fief/

test $UNIT_TESTS="1":
  pytest --cov fief/ --cov-report=term-missing

update-translations:
  pybabel extract --mapping babel.cfg --output-file=fief/locale/messages.pot .
  pybabel update --domain=messages --input-file=fief/locale/messages.pot --output-dir=fief/locale

compile-translations:
  pybabel compile --domain=messages --directory=fief/locale

build-css:
  npm run build:css

create-db:
	docker volume create fief-dev-db
	docker run -p 5432:5432 --name fief-dev-db -e PGDATA=/var/lib/postgresql/data/pgdata -e POSTGRES_USER=fief -e POSTGRES_PASSWORD=fiefpassword -v fief-dev-db:/var/lib/postgresql/data -d postgres:alpine

start-db:
  docker start fief-dev-db

stop-db:
  docker rm -f fief-dev-db

revision-global message: start-db
  alembic -c fief/alembic.ini -n global revision --autogenerate -m "{{message}}"

migrate-global: start-db
  alembic -c fief/alembic.ini -n global upgrade head

revision-account message: start-db
  alembic -c fief/alembic.ini -n account revision --autogenerate -m "{{message}}"

migrate-account: start-db
  alembic -c fief/alembic.ini -n account upgrade head

start: start-db compile-translations build-css
  uvicorn --host 0.0.0.0 --port 8000 fief.app:app

start-watch:
  watchexec -r --exts po,py,html -- just start

bumpversion version:
  bumpversion {{version}}
